buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "nu.studer:gradle-jooq-plugin:6.0.1"
    }
}

apply plugin: "nu.studer.jooq"
apply plugin: "java"

sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

def versions = [
        PostgreJdbcVersion: "42.2.23",
        JooqVersion       : "3.15.3",
]

dependencies {
    jooqGenerator "org.postgresql:postgresql:${versions.PostgreJdbcVersion}"
    implementation "org.jooq:jooq:${versions.JooqVersion}"
}

jooq {
    version = "${versions.JooqVersion}"
    edition = nu.studer.gradle.jooq.JooqEdition.OSS
    configurations {
        main {  // name of the jOOQ configuration
            generationTool {
                jdbc {
                    driver = "org.postgresql.Driver"
                    url = "jdbc:postgresql://localhost:5432/sputniknchat"
                    user = "postgres"
                    password = "ok"
                    schema = "public"
                }
                generator {
                    name = "org.jooq.codegen.DefaultGenerator"
                    strategy {
                        name = "org.jooq.codegen.DefaultGeneratorStrategy"
                    }
                    database {
                        name = "org.jooq.meta.postgres.PostgresDatabase"
                        inputSchema = "public"
                    }
                    generate {
                        relations = true
                        deprecated = false
                        records = true
                        immutablePojos = true
                        fluentSetters = true
                        daos = false
                    }
                    target {
                        packageName = "net.orionlab.sputniknchatserver.db"
                        directory = "src/generated/java"
                    }
                }
            }
        }
    }
}

ext {
    chatServerGenOutDir = "../sputnikn-chat-server/src/generated"
}


task("removeGenerated", type: Delete) {
    delete "src/generated", "${chatServerGenOutDir}/java/net/orionlab/sputniknchatserver/db"
}

task("copyGenerated", type: Copy) {
    from "src/generated"
    into "${chatServerGenOutDir}"
}

generateJooq.enabled = project.hasProperty("gendb")
generateJooq.dependsOn(removeGenerated).finalizedBy(copyGenerated)
